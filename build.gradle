
plugins {
    id 'application'
    id "org.moditect.gradleplugin" version "1.0.0-rc3"
    id 'org.beryx.jlink' version '2.22.3'
}

setVersion '2.1.4'

if (!file("${projectDir}/pdxu_launcher.properties").exists()) {
    file("${projectDir}/pdxu_launcher.properties").write(file("${projectDir}/pdxu_launcher.properties.default").getText())
}

java {
    modularity.inferModulePath = true

    // Use JDK 15 to avoid this bug: https://bugs.openjdk.java.net/browse/JDK-8235915
    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15
}

application {
    mainModule = 'com.crschnick.pdx_unlimiter.updater'
    mainClass = 'com.crschnick.pdx_unlimiter.updater.Updater'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "2.11.3"
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0-alpha1'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.0-alpha1'
    implementation files("$buildDir/generated-modules/commons-io-2.8.0.jar")
    implementation files("$buildDir/generated-modules/commons-lang3-3.11.jar")
    implementation files("$buildDir/generated-modules/sentry-1.7.30.jar")
}

addDependenciesModuleInfo {
    overwriteExistingFiles = true
    jdepsExtraArgs = ['-q']
    outputDirectory = file("$buildDir/generated-modules")
    modules {
        module {
            artifact 'org.apache.commons:commons-lang3:3.11'
            moduleInfoSource = '''
                module org.apache.commons.lang3 {
                    requires java.se;
                    exports org.apache.commons.lang3;
                    exports org.apache.commons.lang3.arch;
                    exports org.apache.commons.lang3.reflect;
                }
            '''
        }
        module {
            artifact 'commons-io:commons-io:2.8.0'
            moduleInfoSource = '''
                module org.apache.commons.io {
                    requires java.se;
                    exports org.apache.commons.io;
                    exports org.apache.commons.io.file;
                }
            '''
        }
        module {
            artifact 'io.sentry:sentry:1.7.30'
            moduleInfoSource = '''
                module io.sentry {
                    exports io.sentry;        
                    exports io.sentry.context;    
                    exports io.sentry.event;
                    requires org.slf4j;
                    requires java.naming;
                    requires com.fasterxml.jackson.core;
                }
            '''
        }
    }
}

task copyModules(type: Copy) {
    into "${buildDir}/modules"
    from configurations.runtimeClasspath
}

task copyOutput(type: Copy) {
    into "${buildDir}/modules"
    from "${buildDir}/libs"
}

build.dependsOn(copyModules)
build.dependsOn(copyOutput)

jlink {
    imageDir = file("$buildDir/image")
    options = [
            // '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages']
    launcher {
        name = 'Pdx-Unlimiter'
    }

    customImage {
        appModules = ['com.crschnick.pdx_unlimiter.updater', 'org.slf4j', 'org.slf4j.simple', 'jdk.crypto.ec']
    }

    jpackage {
        imageOutputDir = file("$buildDir/dist")
        installerOutputDir = file("$buildDir/bin")
        imageName = 'Pdx-Unlimiter'
        skipInstaller = false
        applicationName = 'Pdx-Unlimiter'
        installerOptions.addAll(
                '--temp', "$buildDir/tempfiles",
                '--resource-dir', "$projectDir/res",
                '--verbose',
                '--copyright','(C) 2020, Christopher Schnick',
                '--license-file','res/license_file.txt',
                '--vendor','Pdx-Unlimiter',
                '--description', 'A tool aimed at improving the gameplay experience for various Paradox Games.',
                '--file-associations', 'res/ck3.properties',
                '--file-associations', 'res/eu4.properties',
                '--file-associations', 'res/hoi4.properties',
                '--file-associations', 'res/sav.properties'
        )
    }
}


import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

task createDist(type: DefaultTask)
OperatingSystem os = DefaultNativePlatform.currentOperatingSystem;
if (os.windows) {
    // To fix this bug: https://bugs.openjdk.java.net/browse/JDK-8254920
    task copyZipDll(type: Copy) {
        into "${buildDir}/dist/Pdx-Unlimiter"
        from "${buildDir}/dist/Pdx-Unlimiter/runtime/bin/zip.dll"
    }

    jpackageImage.finalizedBy(copyZipDll)

    task writePackageVersion(type: DefaultTask) {
        doLast {
            file("${buildDir}/dist/Pdx-Unlimiter/runtime/version").write(version)
        }
    }
    jpackageImage.finalizedBy(writePackageVersion)

    task copyElevate(type: Copy) {
        into "${buildDir}/dist/Pdx-Unlimiter/runtime/bin/"
        from "${projectDir}/res/Elevate.exe"
    }
    jpackageImage.finalizedBy(copyElevate)

    jlink {
        jpackage {
            icon = "imgs/logo.ico"
            installerType = 'msi'
            installerOptions.addAll(
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-shortcut',
                    '--win-menu-group', 'Pdx-Unlimiter')
        }
    }

    task signInstaller(type: Exec) {
        workingDir "$projectDir"
        commandLine 'sign\\signtool.exe sign /f sign\\certificate.cer /fd sha256 /tr http://timestamp.comodoca.com?td=sha256 /td sha256 /v build\\bin\\Pdx-Unlimiter-2.0.msi'.split(" ")

    }
    jpackage.finalizedBy(signInstaller)

    task makeWritable(type: DefaultTask) {
        doLast {
            file('build\\dist\\Pdx-Unlimiter\\Pdx-Unlimiter.exe').writable = true
        }
    }

    task signExecutable(type: Exec, dependsOn: makeWritable) {
        workingDir "$projectDir"
        commandLine 'sign\\signtool.exe sign /f sign\\certificate.cer /fd sha256 /tr http://timestamp.comodoca.com?td=sha256 /td sha256 /v /as build\\dist\\Pdx-Unlimiter\\Pdx-Unlimiter.exe'.split(" ")

    }
    jpackageImage.finalizedBy(signExecutable)
} else {
    task writePackageVersion(type: DefaultTask) {
        doLast {
            file("${buildDir}/dist/Pdx-Unlimiter/lib/runtime/version").write(version)
        }
    }
    jpackageImage.finalizedBy(writePackageVersion)

    jlink {
        jpackage {
            installerType = 'deb'
            installerOptions.addAll(
                    '--linux-menu-group', 'Game;Utility;',
                    '--linux-shortcut')
        }
    }
}


task writeImageVersion(type: DefaultTask, dependsOn: jpackage) {
    doLast {
        file("${buildDir}/image/version").write(version)
    }
}

task zipImage(type: Zip, dependsOn: writeImageVersion) {
    destinationDirectory = file("$buildDir/dist")
    archiveAppendix = os.windows ? "windows" : "linux"
    archiveBaseName = "pdxu_launcher"
    archiveVersion = ""
    from "${buildDir}/image"
}

createDist.finalizedBy(zipImage, jpackage)
